set logtype text
capture log close
local logdate = string( d(`c(current_date)'), "%dCYND" )

******************************************************************
**	Title:		Main file to call analysis files, etc.
**	Author:		Ian McCarthy
**	Date Created:	1/3/2019
**	Date Updated:	2/5/2026
******************************************************************

******************************************************************
** Paths
set more off
global ROOT_PATH "/home/imc969/files/dua_027710/"
global DATA_SAS "${ROOT_PATH}data-sas/"
global DATA_UPLOAD "${ROOT_PATH}data-external/"

global PROJ_PATH "/home/imc969/files/dua_027710/pcp-referrals/"
global CODE_FILES "${PROJ_PATH}analysis/"
global LOG_PATH "${PROJ_PATH}logs/"
global RESULTS_ROOT "${PROJ_PATH}results/"
cd "${ROOT_PATH}stata-ado"


******************************************************************
** Global Variables

** referral assignment
global PCP_First=1	/* limit E&M and Claim Referring physicians to PCPs before identifying referral */
global PCP_Only=1	/* require that referring physician is a PCP */
global RFR_Priority=0   /* look to the listed referring physician on the claim as first indicator of referring physician */

if ${PCP_First}==1 {
	global PCP_Only=1
}

** global variables governing level of decision maker (physician vs practice)
global PCP_Practice=1   /* 0 denotes decision between physician and physician, 1 denotes decision at physician to practice */

** file paths based on global values
if ${PCP_Practice}==1 {
	global DATA_FINAL "${PROJ_PATH}data/pcp-practice-level/"
	global RESULTS_FINAL "${PROJ_PATH}results/pcp-practice-level/"
} 
else if ${PCP_Practice}==0 {
	global DATA_FINAL "${PROJ_PATH}data/pcp-level/"
	global RESULTS_FINAL "${PROJ_PATH}results/pcp-level/"	
}


** choice sets and sizes
global OUTSIDE_OPTION=0
global SPEC_MIN=20

** time-varying congestion
global CONG_t=1
if ${CONG_t}==1 {
	global RESULTS_FINAL "${RESULTS_FINAL}time_vary/"
}


******************************************************************
** Preliminary code

do "${CODE_FILES}A0-programs.do"		/* Programs and functions */
do "${CODE_FILES}A0-intermediate-data.do"	/* Temporary datasets used throughout analysis */
		
		
******************************************************************
** Analysis

do "${CODE_FILES}A1-desc-stats.do"
do "${CODE_FILES}A2-reduced-form.do"
do "${CODE_FILES}A3-mnl-myopic.do"
do "${CODE_FILES}A4-mnl-distance.do"
do "${CODE_FILES}A5-structural-myopic.do"
do "${CODE_FILES}A6-structural-forward-looking.do"

******************************************************************		
** Output Results

global GRAPHS_ONLY 0

* Initialize structural numbers file
local numfile "${RESULTS_FINAL}paper-numbers-structural.tex"
file open numfh using "`numfile'", write replace
file write numfh "%% Auto-generated by O2-structural-summary.do â€” do not edit by hand" _n
file close numfh

global MODEL_TYPE "Myopic"
do "${CODE_FILES}O2-structural-summary.do"

global MODEL_TYPE "FWD"
do "${CODE_FILES}O2-structural-summary.do"


******************************************************************		
** Final Diagnostics

* MLE convergence
local r_type="${PCP_First}_${PCP_Only}_${RFR_Priority}"
use "${RESULTS_FINAL}StructureMyopicHRR_CoefFull_`r_type'_rhobar.dta", clear
**use "${RESULTS_FINAL}StructureForwardHRR_CoefFull_`r_type'_rhobar.dta", clear

gen eta_val=coef_val if coef_name=="eta"
gen hrr_val=coef_val if coef_name=="hrr"
gen conv_val=coef_val if coef_name=="converged"
gen ll_val=coef_val if coef_name=="log_like"
destring coef_name, gen(test_numeric) force
gen group_id=sum(coef_val==0 & coef_se==0 & test_numeric!=.)
bys group_id: egen hrr=min(hrr_val)
bys group_id: egen eta=min(eta_val)
bys group_id: egen converged=min(conv_val)
bys group_id: egen log_like=min(ll_val)
drop eta_val hrr_val conv_val ll_val group_id test_numeric

keep if coef_name=="belief_s" | coef_name=="o.belief_s"
rename coef_val coef_m
replace coef_m=0 if coef_m==.

bys hrr eta: gen est_obs=_n
keep if est_obs==1
keep eta hrr log_like converged coef_m
bys eta: tab converged
keep hrr eta coef_m
rename coef_m alpha_myopic
save temp_myopic_alpha, replace


local r_type="${PCP_First}_${PCP_Only}_${RFR_Priority}"
use "${RESULTS_FINAL}StructureForwardHRR_CoefFull_`r_type'_rhobar.dta", clear

gen eta_val=coef_val if coef_name=="eta"
gen hrr_val=coef_val if coef_name=="hrr"
gen conv_val=coef_val if coef_name=="converged"
gen ll_val=coef_val if coef_name=="log_like"
destring coef_name, gen(test_numeric) force
gen group_id=sum(coef_val==0 & coef_se==0 & test_numeric!=.)
bys group_id: egen hrr=min(hrr_val)
bys group_id: egen eta=min(eta_val)
bys group_id: egen converged=min(conv_val)
bys group_id: egen log_like=min(ll_val)
drop eta_val hrr_val conv_val ll_val group_id test_numeric

keep if coef_name=="belief_s" | coef_name=="o.belief_s"
rename coef_val coef_m
replace coef_m=0 if coef_m==.

bys hrr eta: gen est_obs=_n
keep if est_obs==1
keep eta hrr log_like converged coef_m
bys eta: tab converged
keep hrr eta coef_m
rename coef_m alpha_fwd
save temp_fwd_alpha, replace


* Counterfactual iteration convergence
use "${RESULTS_FINAL}CounterFactuals_Myopic5.dta", clear
merge m:1 hrr eta using temp_myopic_alpha, nogenerate keep(master match)
gen alpha_0=(alpha_myopic==0)
tab no_equil_full alpha_0
collapse (sum) pr_j, by(alpha_0 no_equil_full)

use "${RESULTS_FINAL}CounterFactuals_Myopic5.dta", clear
merge m:1 hrr eta using temp_myopic_alpha, nogenerate keep(master match)
gen alpha_0=(alpha_myopic==0)
collapse (sum) pr_j alpha_0 no_equil_full, by(hrr)
replace alpha_0=(alpha_0>0)
replace no_equil_full=(no_equil_full>0)
collapse (sum) pr_j, by(alpha_0 no_equil_full)


use "${RESULTS_FINAL}CounterFactuals_FWD5.dta", clear
merge m:1 hrr eta using temp_myopic_alpha, nogenerate keep(master match)
gen alpha_0=(alpha_myopic==0)
tab no_equil_full alpha_0
collapse (sum) pr_j, by(alpha_0 no_equil_full)

use "${RESULTS_FINAL}CounterFactuals_FWD5.dta", clear
merge m:1 hrr eta using temp_myopic_alpha, nogenerate keep(master match)
gen alpha_0=(alpha_myopic==0)
collapse (sum) pr_j alpha_0 no_equil_full, by(hrr)
replace alpha_0=(alpha_0>0)
replace no_equil_full=(no_equil_full>0)
collapse (sum) pr_j, by(alpha_0 no_equil_full)

/*
use "${RESULTS_FINAL}CounterFactualsSpec_Myopic5.dta", clear
merge m:1 hrr eta using temp_myopic_alpha, nogenerate keep(master match)
gen alpha_0=(alpha_myopic==0)
gen no_conv=(no_equil_full>0)
collapse (sum) pred_patients0, by(alpha_0 no_conv)
*/



